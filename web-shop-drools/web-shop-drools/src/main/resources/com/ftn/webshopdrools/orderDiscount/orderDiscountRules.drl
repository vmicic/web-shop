//created on: May 19, 2020
package com.ftn.webshopdrools.orderDiscount

//list any import classes here.
import com.ftn.webshop.domain.Order;
import com.ftn.webshop.domain.OrderLine;
import com.ftn.webshop.domain.Discount;
import com.ftn.webshop.domain.TypeOfDiscount;
import com.ftn.webshop.domain.User;
import com.ftn.webshop.domain.CustomerCategory;


//declare any global variables here

global com.ftn.webshop.services.impl.OrderServiceImpl orderService;
global com.ftn.webshop.services.impl.DiscountServiceImpl discountService;


rule "Calculate order price before discounts"
	salience 200
    when
    	$order: Order($lines: orderLines, priceBeforeDiscount == null)
    	Number($totalPrice: doubleValue) from accumulate(
    		OrderLine($priceTotalFinal: priceTotalFinal)
    		from $lines,
    		sum($priceTotalFinal)
    	) 
    then
        System.out.println("Calculate order price before order discounts");   
        System.out.println("Order: " + $order.getId() + ", price total before discounts(included order line discount) is: " + $totalPrice);
        modify($order) {
        	setPriceBeforeDiscount($totalPrice);
        }
        orderService.save($order);
end

rule "Basic fiscounts over 200000"
	salience 150
	when
		$order: Order(priceBeforeDiscount > 200000, discounts.empty == true)
	then
        System.out.println("Basic discount for order, because order price is bigger than 20000");   
        System.out.println("Price of order: " + $order.getId() + ", is bigger than 200000, adding basic discount");        
        discountService.createDiscount(new Discount($order, 5., TypeOfDiscount.BASIC));
        modify($order) {
        	addDiscount(new Discount($order, 5., TypeOfDiscount.BASIC));
        }
		
end

rule "Additional discount for member more than 2 years"
	salience 100
	lock-on-active
	when
		$order: Order($user: user, percentageDiscount == null)
		User(this before[730d] $order) from $user
	then
		System.out.println("User registered for more than 2 year: " + $user);
		discountService.createDiscount(new Discount($order, 1., TypeOfDiscount.ADDITIONAL));
        modify($order) {
        	addDiscount(new Discount($order, 1., TypeOfDiscount.ADDITIONAL));
        }
end

rule "Additional discount for silver or gold customers"
	salience 100
	lock-on-active
	when
		$order: Order($user: user)
		User(customerCategory.name == "SILVER" || customerCategory.name == "GOLD") from $user
	then		
		System.out.println("User is gold or silver customer: " + $user);
		discountService.createDiscount(new Discount($order, 1., TypeOfDiscount.ADDITIONAL));
        modify($order) {
        	addDiscount(new Discount($order, 1., TypeOfDiscount.ADDITIONAL));
        }
end





