//created on: May 15, 2020
package com.ftn.webshopdrools.additionalDiscountsOrderLine

//list any import classes here.
import com.ftn.webshop.domain.Order;
import com.ftn.webshop.domain.OrderLine;
import com.ftn.webshop.domain.Item;
import com.ftn.webshop.domain.ItemCategory;
import com.ftn.webshop.domain.DiscountForItem;
import com.ftn.webshop.domain.TypeOfDiscount;
import com.ftn.webshop.domain.User;
import com.ftn.webshop.domain.Promotion;
import java.util.List;



//declare any global variables here
global com.ftn.webshop.services.impl.DiscountForItemServiceImpl discountForItemService;




rule "If item bought in last 15 days create 2% discount"
	salience 500
    when    	
    	$order: Order($lines: orderLines, $user: user)
    	$orderLine: OrderLine($item: item, percentageDiscount == null) from $lines
    	exists(
    		$orderOld: Order(this meets[15d] $order) from $user.getOrders() and
    		OrderLine(item.equals($item)) from $orderOld.getOrderLines()
    	)
    then       
        System.out.println("For order: " + $order.getId() + ", item: " + $item.getName() + ", bought within 15 day, orderLine to update: " + $orderLine.getId());   
        discountForItemService.createDiscountForItem(new DiscountForItem($orderLine, 2.0, $order, TypeOfDiscount.ADDITIONAL));
        modify($orderLine) {
        	addDiscountForItem(new DiscountForItem($orderLine, 2.0, $order, TypeOfDiscount.ADDITIONAL));
        }         
     
end

rule "If item from same category bought in last 30 days create 1% discount"
	salience 500
    when    	
    	$order: Order($lines: orderLines, $user: user)
    	$orderLine: OrderLine($category: item.category, percentageDiscount == null) from $lines
    	exists(
    		$orderOld: Order(this meets[30d] $order) from $user.getOrders() and
    		OrderLine(item.category == $category) from $orderOld.getOrderLines()
    	)
    then       
		//System.out.println("If item bought in last 30 days create 1% discount");   
		System.out.println("For order: " + $order.getId() + ", item category: " + $category.getName() + ", bought within 30 days, orderLine to update: " + $orderLine.getId());   
	    discountForItemService.createDiscountForItem(new DiscountForItem($orderLine, 1.0, $order, TypeOfDiscount.ADDITIONAL));
        modify($orderLine) {
        	addDiscountForItem(new DiscountForItem($orderLine, 1.0, $order, TypeOfDiscount.ADDITIONAL));
        }           
end

rule "Promotion discount"
	salience 500
	when
		$promotion: Promotion($percentageDiscount: percentageDiscount)
		$o: Order(this during $promotion)
		$orderLine: OrderLine($category: item.category, percentageDiscount == null) from $o.getOrderLines()
		exists(
			ItemCategory(this == $category) from $promotion.getItemCategories()
		)
	then
		System.out.println("Order during promotion, order date: " + $o.getDate() + ", promotion starting date: " + $promotion.getStartTime() + ", order ending date: " + $promotion.getEndTime()
			+ ", orderLine item category: " + $category.getName());
		discountForItemService.createDiscountForItem(new DiscountForItem($orderLine, $percentageDiscount, $o, TypeOfDiscount.ADDITIONAL));
        modify($orderLine) {
        	addDiscountForItem(new DiscountForItem($orderLine, $percentageDiscount, $o, TypeOfDiscount.ADDITIONAL));
        }    
end


